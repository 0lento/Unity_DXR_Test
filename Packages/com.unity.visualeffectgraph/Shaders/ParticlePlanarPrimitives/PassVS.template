#if defined(HAS_STRIPS) && !defined(VFX_PRIMITIVE_QUAD)
#error VFX_PRIMITIVE_QUAD must be defined when HAS_STRIPS is.
#endif

struct vs_input
{
	VFX_DECLARE_INSTANCE_ID
};

#if HAS_STRIPS
#define PARTICLE_IN_EDGE (id & 1)

uint GetParticleIndex(uint relativeIndex, uint stripIndex)
{
	return PARTICLE_PER_STRIP_COUNT * stripIndex + (relativeIndex + STRIP_DATA(STRIP_FIRST_INDEX)) % PARTICLE_PER_STRIP_COUNT;
}

float3 GetParticlePosition(uint index)
{
	${VFXLoadAttributes:{position}}
	return position;
}

float3 GetStripTangent(float3 currentPos, uint relativeIndex, uint stripIndex)
{
	float3 prevTangent = (float3)0.0f;
	if (relativeIndex > 0)
	{
		uint prevIndex = GetParticleIndex(relativeIndex - 1,stripIndex);
		prevTangent = normalize(currentPos - GetParticlePosition(prevIndex));
	}
	
	float3 nextTangent = (float3)0.0f;
	if (relativeIndex < STRIP_DATA(STRIP_NEXT_INDEX) - 1)
	{
		uint nextIndex = GetParticleIndex(relativeIndex + 1,stripIndex);
		prevTangent = normalize(GetParticlePosition(nextIndex) - currentPos);
	}
	
	return normalize(prevTangent + nextTangent);
}
#endif

#pragma vertex vert
VFX_VARYING_PS_INPUTS vert(uint id : SV_VertexID, vs_input i)
{
	VFX_VARYING_PS_INPUTS o = (VFX_VARYING_PS_INPUTS)0;

	UNITY_SETUP_INSTANCE_ID(i);
	UNITY_INITIALIZE_VERTEX_OUTPUT_STEREO(o);

#if VFX_PRIMITIVE_TRIANGLE
	uint index = id / 3;
#elif VFX_PRIMITIVE_QUAD
#if HAS_STRIPS
	id += VFX_GET_INSTANCE_ID(i) * 8192;
	const uint vertexPerStripCount = (PARTICLE_PER_STRIP_COUNT - 1) << 2;
	uint stripIndex = id / vertexPerStripCount;
	uint currentIndex = ((id % vertexPerStripCount) >> 2) + (id & 1); // relative index of particle
	
	uint maxEdgeIndex = currentIndex - PARTICLE_IN_EDGE + 1;
	if (maxEdgeIndex >= STRIP_DATA(STRIP_NEXT_INDEX))
		return o;
	
	uint index = GetParticleIndex(currentIndex, stripIndex);
#else
	uint index = (id >> 2) + VFX_GET_INSTANCE_ID(i) * 2048;
#endif
#elif VFX_PRIMITIVE_OCTAGON
	uint index = (id >> 3) + VFX_GET_INSTANCE_ID(i) * 1024;
#endif

	${VFXLoadAttributesOrCull}	
	${VFXProcessBlocks}
	
#if !HAS_STRIPS
	if (!alive)
		return o;
#endif
	
#if VFX_PRIMITIVE_QUAD

#if HAS_STRIPS
#if VFX_STRIPS_UV_STRECHED
	o.VFX_VARYING_UV.x = (float)(currentIndex) / (STRIP_DATA(STRIP_NEXT_INDEX) - 1);
#else
	o.VFX_VARYING_UV.x = PARTICLE_IN_EDGE;
#endif

	o.VFX_VARYING_UV.y = float((id & 2) >> 1);
	const float2 vOffsets = float2(0.0f,o.VFX_VARYING_UV.y - 0.5f);
	
	// Orient strips along their tangents
	axisX = GetStripTangent(position, currentIndex, stripIndex);
	axisZ = position - GetViewVFXPosition();
	axisY = normalize(cross(axisZ, axisX));
	axisZ = normalize(cross(axisX, axisY));
	
#else
	o.VFX_VARYING_UV.x = float(id & 1);
	o.VFX_VARYING_UV.y = float((id & 2) >> 1);
	const float2 vOffsets = o.VFX_VARYING_UV.xy - 0.5f;
#endif
	
#elif VFX_PRIMITIVE_TRIANGLE

	const float2 kOffsets[] = {
		float2(-0.5f, 	-0.288675129413604736328125f),
		float2(0.0f, 	0.57735025882720947265625f),
		float2(0.5f,	-0.288675129413604736328125f),
	};
	
	const float kUVScale = 0.866025388240814208984375f;
	
	const float2 vOffsets = kOffsets[id % 3];
	o.VFX_VARYING_UV.xy = (vOffsets * kUVScale) + 0.5f;
	
#elif VFX_PRIMITIVE_OCTAGON	
	
	const float2 kUvs[8] = 
	{
		float2(-0.5f,	0.0f),
		float2(-0.5f,	0.5f),
		float2(0.0f,	0.5f),
		float2(0.5f,	0.5f),
		float2(0.5f,	0.0f),
		float2(0.5f,	-0.5f),
		float2(0.0f,	-0.5f),
		float2(-0.5f,	-0.5f),
	};
	
	${VFXLoadParameter:{cropFactor}}
	cropFactor = id & 1 ? 1.0f - cropFactor : 1.0f;
	const float2 vOffsets = kUvs[id & 7] * cropFactor;
	o.VFX_VARYING_UV.xy = vOffsets + 0.5f;
	
#endif
	
	${VFXLoadSize}
	
	const float4x4 elementToVFX = GetElementToVFXMatrix(axisX,axisY,axisZ,float3(angleX,angleY,angleZ),float3(pivotX,pivotY,pivotZ),size3,position);
	float3 inputVertexPosition = float3(vOffsets, 0.0f);
	float3 vPos = mul(elementToVFX,float4(inputVertexPosition, 1.0f)).xyz;

	o.VFX_VARYING_POSCS = TransformPositionVFXToClip(vPos);

	float3 normalWS = normalize(TransformDirectionVFXToWorld(normalize(-transpose(elementToVFX)[2].xyz)));
	#ifdef VFX_VARYING_NORMAL
	float normalFlip = (size3.x * size3.y * size3.z) < 0 ? -1 : 1;
	o.VFX_VARYING_NORMAL = normalFlip * normalWS;
	#endif
	#ifdef VFX_VARYING_TANGENT
	o.VFX_VARYING_TANGENT = normalize(TransformDirectionVFXToWorld(normalize(transpose(elementToVFX)[0].xyz)));
	#endif
	#ifdef VFX_VARYING_BENTFACTORS
	${VFXLoadParameter:{bentNormalFactor}}
	#if HAS_STRIPS
	#define BENT_FACTOR_MULTIPLIER 2.0f
	#else
	#define BENT_FACTOR_MULTIPLIER 1.41421353816986083984375f
	#endif
	o.VFX_VARYING_BENTFACTORS = vOffsets * bentNormalFactor * BENT_FACTOR_MULTIPLIER;
	#endif
	
	${VFXVertexComputeCurrentAndPreviousClipPos}

	${VFXVertexCommonProcess}
	
	${VFXVertexSetFlipbooksInterpolants}

	${VFXVertexAdditionalProcess}

	return o;
}
